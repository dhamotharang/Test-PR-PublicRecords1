import HealthCareProvider;
EXPORT ManualCollapse (DATASET (HealthCareProvider.Layout_HealthProvider.HealthCareProvider_Header) hdr = HealthCareFacility.Files.Facility_Salt_Output_DS, DATASET ({UNSIGNED8 BEFORE_LNPID, UNSIGNED8 AFTER_LNPID}) CDS) := FUNCTION
	
	De_Cds := DEDUP (SORT(CDS (BEFORE_LNPID > 0 AND AFTER_LNPID > 0),BEFORE_LNPID,AFTER_LNPID),BEFORE_LNPID,AFTER_LNPID);

	NPI_UPIN_REC := RECORD
		UNSIGNED8 BEFORE_LNPID;
		UNSIGNED8 AFTER_LNPID;
		STRING10 NPI_NUMBER;
	END;

	ADD_NPI := JOIN (HDR,De_Cds,LEFT.LNPID = RIGHT.BEFORE_LNPID,TRANSFORM(NPI_UPIN_REC, SELF := LEFT; SELF := RIGHT;));
	
	DE_ADD_NPI := DEDUP(SORT (ADD_NPI,AFTER_LNPID,NPI_NUMBER),AFTER_LNPID,NPI_NUMBER);

	U_NPI 	:= TABLE (DE_ADD_NPI,{AFTER_LNPID, CNT := COUNT(GROUP)},AFTER_LNPID,MERGE);
	
	Re_NPI := JOIN (DE_CDS,U_NPI (CNT = 1),LEFT.AFTER_LNPID = RIGHT.AFTER_LNPID,TRANSFORM({UNSIGNED8 BEFORE_LNPID, UNSIGNED8 AFTER_LNPID}, SELF := LEFT;));

	U_CDS := PROJECT (DEDUP(SORT (Re_NPI,BEFORE_LNPID,AFTER_LNPID),BEFORE_LNPID,AFTER_LNPID),{UNSIGNED8 BEFORE_LNPID, UNSIGNED8 AFTER_LNPID});
	
	Rec_Cnt := COUNT (De_Cds);
	
	J0 := JOIN (Hdr,De_Cds,LEFT.LNPID = RIGHT.BEFORE_LNPID,TRANSFORM(HealthCareProvider.Layout_HealthProvider.HealthCareProvider_Header, 
			SELF.LNPID := IF (LEFT.LNPID = RIGHT.BEFORE_LNPID,RIGHT.AFTER_LNPID,LEFT.LNPID);
			SELF := LEFT;
			),LEFT OUTER, LOOKUP);
	
	J1 := JOIN (DISTRIBUTE(Hdr,HASH32(LNPID)),DISTRIBUTE(De_Cds,HASH32(BEFORE_LNPID)),LEFT.LNPID = RIGHT.BEFORE_LNPID,TRANSFORM(HealthCareProvider.Layout_HealthProvider.HealthCareProvider_Header, 
			SELF.LNPID := IF (LEFT.LNPID = RIGHT.BEFORE_LNPID,RIGHT.AFTER_LNPID,LEFT.LNPID);
			SELF := LEFT;
			),LEFT OUTER,  LOCAL);

	Manual_Collapse := IF (REC_CNT > 10000,J1,J0);
	
	OUTPUT (COUNT(De_Cds),NAMED('Raw_Bef_Aft_LNPID'));
	OUTPUT (COUNT(U_CDS),NAMED('Cleaned_Bef_Aft_LNPID'));
	RETURN (DISTRIBUTE (Manual_Collapse, HASH32(LNPID)));
END;

