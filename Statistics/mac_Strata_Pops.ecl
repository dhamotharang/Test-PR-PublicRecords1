/*
	Automatically generate standard STRATA population stats based on the layout of the passed in dataset
	makes it easier to generate and/or run these types of stats
	If pOutputEcl is set to 'true', it will output the ecl generated by the macro as a string, so it can 
		be copy-pasted into an attribute or builder window and run(useful for generating static population stats)
	Else, it will generate the ecl and then run it, generating the stats themselves.
*/
export mac_Strata_Pops(

	 pDataset											// input dataset
	,pOutput											// Named output to run ecl generated by macro
	,pGroupByField		= '\'\''		// Field that the table should be grouped on
	,pOutputEcl				= 'false'		// Should output the ecl as a string(for testing) or actually run the ecl
	,pShouldExport		= 'true'		// should export output parameter(so can access outside of a module)
	,pSetRemoveFields	= '[]'			// remove these fields from population stats
	
) :=
macro

	LOADXML('<xml/>');
	#EXPORTXML(pDataset_MetaInfo, recordof(pDataset))

	#uniquename(ldataset					)
	#uniquename(loutput						)
	#uniquename(fieldname					)
	#uniquename(groupbyfield			)
	#uniquename(name							)
	#uniquename(named_layout			)
	#uniquename(stringfiller			)
	#uniquename(lenName						)
	#uniquename(fillernonzero			)
	#uniquename(fillernonblank		)
	#uniquename(fillertrue				)
	#uniquename(moutput						)
	#uniquename(dDataset					)
	#uniquename(Layout_pInput_stat)
	#uniquename(pInput_stat				)
	#uniquename(pInput_prep				)

//	string75 %stringfiller%;
	#SET(stringfiller, '                                                                           ')
	#SET(ldataset	,trim(#TEXT(pDataset),all))

	#if(pShouldExport = true)
		#SET(moutput	,'export ' + trim(#TEXT(pOutput),all))
	#ELSE
		#SET(moutput	,trim(#TEXT(pOutput),all))
	#END

	#if(pOutputEcl = true)
		#SET(dDataset						,'pDataset')
		#SET(Layout_pInput_stat	,'Layout_pInput_stat')
		#SET(pInput_stat				,'pInput_stat')
		#SET(pInput_prep				,'pInput_prep')
	#END

	#SET(loutput	,%'dDataset'% + ' := ' + %'ldataset'% + ';\n')
	
	#IF(pGroupByField	= '')
		#APPEND(loutput	,%'pInput_prep'% + ' := project(' + %'dDataset'% + ',transform({recordof(' + %'dDataset'% + '),unsigned1 const1 := 0}, self := left));\n');
	#ELSE
		#APPEND(loutput	,%'pInput_prep'% + ' := ' + %'dDataset'% + ';\n');
	#END
	#APPEND(loutput	,%'Layout_pInput_stat'% + '  :=\n')
	#APPEND(loutput	,'record\n')
	#APPEND(loutput	,'	unsigned8 CountGroup  := count(group);\n')
	#IF(pGroupByField	= '')
		#APPEND(loutput	,'	' + %'pInput_prep'% + '.const1;\n')
		#SET(groupbyfield	,'const1')
	#ELSE
		#APPEND(loutput	,'	' + %'pInput_prep'% + '.' + pGroupByField + ';\n')
		#SET(groupbyfield	,pGroupByField)
	#END

	#SET(named_layout	, '')
	#SET(name					, '')
	#if(pOutputEcl = true)
		#SET(fieldname		, '')
	#END
	#FOR (pDataset_MetaInfo)
		#FOR (Field)
			#IF(%'@isRecord'% = '1')
				#SET(named_layout, %'@name'%)
			#ELSE
				#IF(%'named_layout'% = %'@name'% and %'@type'% = '')
					#SET(named_layout, '')
				#ELSE
					#IF(%'named_layout'% != '')
						#SET(name, %'named_layout'% + '.' + %'@name'%)
						#SET(fieldname, %'named_layout'% + '_' + %'@name'%)
					#ELSE
						#SET(name, %'@name'%)
						#SET(fieldname, %'@name'%)
					#END
					
					#SET(lenName				,length(trim(%'fieldname'%,left,right)))
					#SET(fillernonzero	,%'stringfiller'%[1..(75 - (%lenName% - 1	))])
					#SET(fillernonblank	,%'stringfiller'%[1..(75 - (%lenName%			))])
					#SET(fillertrue			,%'stringfiller'%[1..(75 - (%lenName% - 4	))])
					
					#IF(%'name'% != pGroupByField and %'name'% not in pSetRemoveFields)
						#IF(%'@type'% = 'integer' or %'@type'% = 'unsigned')
							#APPEND(loutput, '	unsigned8 ' + %'fieldname'% + '_CountNonZero' + %'fillernonzero'%		+ ' := sum(group, if(' + %'pInput_prep'% + '.' + %'name'% + %'fillernonblank'%	+ ' <> 0   ,1,0));\n');
						#ELSEIF(%'@type'% = 'string')                                                                                                         
							#APPEND(loutput, '	unsigned8 ' + %'fieldname'% + '_CountNonBlank'+ %'fillernonblank'%	+ ' := sum(group, if(' + %'pInput_prep'% + '.' + %'name'% + %'fillernonblank'%	+ ' <> \'\'  ,1,0));\n');
						#ELSEIF(%'@type'% = 'boolean')                                                                                                        
							#APPEND(loutput, '	unsigned8 ' + %'fieldname'% + '_CountTrue'    + %'fillertrue'%			+ ' := sum(group, if(' + %'pInput_prep'% + '.' + %'name'% + %'fillernonblank'%	+ '  = true,1,0));\n');
						#END
					#END
				#END
			#END
		#END
	#END

	#APPEND(loutput, 'end;\n');
	#APPEND(loutput, %'pInput_stat'% + ' := table(' + %'pInput_prep'% + ', ' + %'Layout_pInput_stat'% + ' ,' + %'groupbyfield'% + '  , few);\n');
	#APPEND(loutput, %'moutput'% + ' := ' + %'pInput_stat'% + ';\n');
	
	#if(pOutputEcl = true)
		pOutput := output(%'loutput'%);
	#ELSE
		%loutput%;
	#END

endmacro;