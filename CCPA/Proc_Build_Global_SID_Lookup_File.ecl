IMPORT _control, Std;

	//version: 			build date
	//eclsourceip: 	linux box used to store orbit global_sid file
	//thor_name: 		thor used for build

EXPORT Proc_Build_Global_SID_Lookup_File(STRING version, CONST VARSTRING eclsourceip, STRING thor_name):= FUNCTION

	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	//Spray Orbit File///////////////////////////////////////////////////////////////////////////////////////////////
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	sprayOrbitFile 		:= CCPA.Spray_Global_SID_In(version, eclsourceip, thor_name);
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////	
	//Use Global_SID Lookup Base File Name///////////////////////////////////////////////////////////////////////////
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	filename					:= '~thor_data400::base::ccpa::global_sid_lookup';
																												
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////																											
	//Build Lookup Base//////////////////////////////////////////////////////////////////////////////////////////////
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	buildBase					:= OUTPUT(CCPA.Map_Global_SID_Lookup_File,, filename+'_'+version, OVERWRITE, __COMPRESSED__);
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	//Clear Delete Files/////////////////////////////////////////////////////////////////////////////////////////////
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	clearDelete 			:= NOTHOR(fileservices.clearsuperfile(filename+'_delete', TRUE));	
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	//Move Base Files////////////////////////////////////////////////////////////////////////////////////////////////
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	moveBase					:= Std.File.PromoteSuperFileList([filename,
																											filename+'_father',
																											filename+'_grandfather',
																											filename+'_delete'], filename+'_'+version, TRUE);
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	//Email Build Status/////////////////////////////////////////////////////////////////////////////////////////////
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
			
			emailDOps			:= ';darren.knowles@lexisnexisrisk.com; charlene.ros@lexisnexisrisk.com';
			emailDev			:= ';robert.berger@lexisnexisrisk.com; judy.tao@lexisnexisrisk.com';
			emailTarget		:= _control.MyInfo.EmailAddressNotify + emailDev;
	
	emailBuildNotice 	:= IF(COUNT(CCPA.File_Global_SID.lookupTable) > 0
																,fileservices.SendEmail(emailTarget, 'CCPA: New Consolidated Global_SID Lookup File', 'There is a new Consolidated Global_SID Lookup File available.  Please see: ' + 'http://prod_esp.br.seisint.com:8010/WsWorkunits/WUInfo?Wuid='+ workunit + '&Widget=WUDetailsWidget#/stub/Results-DL/Grid')
																,fileservices.SendEmail(emailTarget, 'CCPA: No New Consolidated Global_SID Lookup File', 'There were no new Global_SIDs available')
																);		
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	//Run Build//////////////////////////////////////////////////////////////////////////////////////////////////////
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	RETURN SEQUENTIAL(sprayOrbitFile,
										buildBase,
										clearDelete,
										moveBase,
										emailBuildNotice);

END;