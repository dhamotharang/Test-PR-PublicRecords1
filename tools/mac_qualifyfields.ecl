/*
	fully qualify fields in a layout, mydataset.did,mydataset.bdid, etc
	for use in an index declaration so you can build that index.
*/
export mac_qualifyfields(
	 pDataset														// input dataset
	,pLayout														// Layout of keys to fully qualify
	,pOutput														// Named output to run ecl generated by macro
	,pOutputEcl								= 'false'	// Should output the ecl as a string(for testing) or actually run the ecl
	
) :=
macro
	
	LOADXML('<xml/>');
	#EXPORTXML(pDataset_MetaInfo, pLayout)
	#uniquename(name							)
	#uniquename(named_layout			)
	#uniquename(moutput						)

	#SET(moutput, '')
	#SET(named_layout	, '')
	#SET(name					, '')
	#FOR (pDataset_MetaInfo)
		#FOR (Field)
			#IF(%'@isRecord'% = '1')
				#SET(named_layout, %'@name'%)
			#ELSE
				#IF(%'named_layout'% = %'@name'% and %'@type'% = '')
					#SET(named_layout, '')
				#ELSE
					#IF(%'named_layout'% != '')
						#SET(name, %'named_layout'% + '.' + %'@name'%)
					#ELSE
						#SET(name			, %'@name'%)
					#END

					#if(%'@position'% != '0')
						#APPEND(moutput	,',')
					#ELSE
						#APPEND(moutput	,'{')
					#END
					#APPEND(moutput	,#TEXT(pDataset) + '.' + %'name'%)
				#END
			#END
		#END
	#END
	#APPEND(moutput	,'}')
	
	#if(pOutputEcl = true)
		pOutput := %'moutput'%;
	#ELSE
		pOutput := %moutput%;
	#END
endmacro;
