IMPORT _control,ut,wk_ut,Data_Services,STD;

a_esp 						:= _control.IPAddress.aprod_thor_esp;
p_esp 						:= _control.IPAddress.prod_thor_esp;

// ********************************************************* <-- CHANGE CONFIG ABOVE

w_s	:='W'+ut.date_math(workunit[2..9],-1)+'-000000';
w_e		:='W'+ut.date_math(workunit[2..9], 1)+'-000000';
gwl_a(string str) := choosen(sort(wk_ut.get_WorkunitList(w_s,w_e,pJobname :=str ,pesp :=  a_esp),-wuid)(state<>'wait'),1);
gwl_p(string str) := choosen(sort(wk_ut.get_WorkunitList(w_s,w_e,pJobname :=str ,pesp :=  p_esp),-wuid)(state<>'wait'),1);

// ********************************************************* <-- CHANGE CONFIG BELOW ONLY

fList0 :=  gwl_a('Prod xIDL Lab Build') 											// (InsuranceHeader ExternalLinking V2 Build Completed)
					+gwl_a('InsuranceHeader PostBuilds Master')
					+gwl_p('*QUICK*')
					+gwl_p('*TransunionCred*')
					+gwl_p('*Risk Indicator*')
					+gwl_p('*VendorSrc load*')
					+gwl_p('*Watchdog*')
					+gwl_p('*PersonHeader Weekly Raw Header*')
					+wk_ut.get_WorkunitList('W20161017-093658','W20161017-093658',,pesp :=  p_esp)
				;

// ********************************************************* <-- CHANGE CONFIG ABOVE ONLY
// ********************************************************* <-- NO NEED TO CHANGE CODE BELOW TO ADD / REMOVE ITEMS TO LIST
fList := project(fList0, {string addl_info:=''} OR {fList0});

reported_status(string state) := case(state, 'failed' => 'has ', 'completed' => 'has ', 'is ')+state;
fList addText(fList L, fList R):= TRANSFORM // 1 left is blank
	SELF.addl_info := L.addl_info + '\n- '+R.wuid+' ('+R.Job+') '+reported_status(R.state)+' ('+R.owner+')'+R.addl_info;
	SELF := R;
END;

tReport := iterate(fList,addText(LEFT,RIGHT));
report := tReport[count(tReport)].addl_info;
bText :=  'Please find the latest status below (only WUIDs with start time after midnight the day before yesterday):'
				 +'\n-------------------------------------------------------------------------------------------'
				 +'\n'
				 +report
				 +'\n'
				 +'\n-------------------------------------------------------------------------------------------'
				 +'\nTo change this list, please review: header.build_monitor_wuid_report'
				 +'\nThis report was generated by '+workunit+ ' (on '+_control.ThisEnvironment.Name+').';

lemail(string emailList) := STD.System.Email.SendEmail(
				emailList ,							// recipientAddress
				'Status Update on Workunits Being Monitored',  		// subjectText
				bText																							// bodyText
				);

GetTimeDate:=Std.Date.SecondsToString(Std.Date.CurrentSeconds(TRUE), '%F%H%M%S%u');
go(string emailList) := sequential( 
						output(fList,named('wuid_being_monitored'))
						,if(count(fList0)>0,
																parallel(
																					 output(bText, named('Message_Sent'))
																					,output(GetTimeDate,named('Latest_Message'))
																					,lemail(emailList)
															  ),
																output(GetTimeDate+'No active Workunits are being monitored',named('Latest_Skip'))
						 )
);

EXPORT build_monitor_wuid_report(string emailList) := go(emailList);