IMPORT Text;

PATTERN ws := text.ws;
PATTERN punct := PATTERN('[.,(\\/]');
PATTERN wsp := opt(ws) (punct | ws) opt(ws);
PATTERN word_as := wsp 'AS';
PATTERN trustee_end :=  wsp ((opt('RELATING' ws) 'TO') | ('ON' ws 'BEHALF' ws 'OF') | 'OF' | 'FOR' | 'UNDER');
PATTERN just_trustee := opt('CO' opt('-')) 'TRUSTEE' opt('S'); 
PATTERN simple_trustee := just_trustee trustee_end;
PATTERN trustee := wsp opt (('A' | 'AN') ws) (((('OWNER' | 'POOL' | 'CORPORATE' | 'INDENTURE' | 'COLLATERAL' | 'CERTIFICATE' | 'BOND' | 'SECURITY' | 'LOAN' | 'SUCCESSOR') ws)+ just_trustee) | simple_trustee);
PATTERN successor := wsp 'SUCCESSOR' opt('S') (ws | '-') ('OF' | 'BY' | 'IN' | 'TO' | 'THROUGH');
PATTERN conservator := wsp 'CONSERVATOR' ws ('OF' | 'FOR');
PATTERN receiver := wsp ('RECEIVER' | 'RECIEVER') ((ws ('OF' | 'FOR')) | conservator);
PATTERN liquidator := ws 'LIQUIDATOR' ws 'OF';
PATTERN subsidiary := wsp opt('A' ws) opt('WHOLLY' ws 'OWNED' ws) 'SUBSIDIARY' ws 'OF';
PATTERN servicer := wsp opt(('SPECIAL' | 'SURETY') ws) 'SERVICER' ws 'FOR';
PATTERN fka := wsp ('FKA' | 'AKA' | ('F' punct 'K' punct 'A') | ('A' punct 'K' punct 'A') | 'FORMERLY KNOWN AS');
PATTERN dba := wsp ('DBA' | ('D' punct 'B' punct 'A') | 'DOING BUSINESS AS');
PATTERN acting := wsp 'ACTING' ws ('BY' | 'THROUGH');
PATTERN just_agent := opt ('CO' opt('-')) 'AGENT';
PATTERN simple_agent := just_agent ((wsp ('OF' | 'FOR' | 'UNDER' | ('OR' ws simple_trustee))) | acting);
PATTERN agent := wsp opt(('A' | 'AN') ws) ((('COLLATERAL' | 'SECURITY' | 'ADMINISTRATIVE' | 'ADMIN' | 'PROGRAM' | 'FUNDING' | 'BANK' | 'DISBURSEMENT' | 'ESCROW') ws just_agent) | simple_agent);
PATTERN custodian := wsp 'CUSTODIAN' (receiver | agent | (wsp opt('OR' ws) just_trustee) | (wsp ('OF' | 'FOR')));
PATTERN misc := ws PATTERN('[^ \t]+'); // non-whitespace sequence
PATTERN how1 := wsp ('INDIVIDUALLY' | 'INDIV' | 'INDV');
PATTERN how2 := wsp 'FOR' ws ('ITSELF' | 'HIMSELF' | 'HERSELF');
PATTERN capacity := wsp 'IN' ws ('ITS' | 'IT\'S' | 'ITS\'' | 'HIS') ws opt(('CORPORATE' | 'INDIVIDUAL' ) ws) 'CAPACITY';
PATTERN solely := wsp 'SOLELY';
PATTERN andamp :=  wsp ('AND' | '&');
PATTERN pre_as_neg := wsp 'NOT' (capacity | how1 | how2) wsp ('BUT') opt(solely);
PATTERN pre_as_pos := (how1 andamp) | (opt(solely) (opt(how1 andamp) capacity | how2) opt(andamp));
PATTERN pre_as := pre_as_neg | pre_as_pos;
PATTERN without_as := custodian | agent | trustee | successor | servicer | subsidiary | receiver | conservator | liquidator | agent;
export PATTERN Pattern_Parse_Co_Name := fka | dba | acting | (pre_as? word_as misc? misc? without_as) | without_as;