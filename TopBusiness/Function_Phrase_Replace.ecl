export Function_Phrase_Replace(
	dataset({string120 clean_company_name;string120 repl_phrase;}) unique_company_names_1) := function
	
	unique_company_names := distribute(unique_company_names_1,hash64(clean_company_name));

	pattern p_word := pattern('[^ ]+');
	pattern p_ws := pattern('[ ]+');
	pattern p_phrase := p_word (p_ws p_word)*;
	pattern p_search := (p_phrase after (first or p_ws)) before (p_ws or last);

	tempparserec := record
		unique_company_names.clean_company_name;
		string phrase := matchtext(p_phrase);
		unsigned startpos := matchposition(p_phrase);
		unsigned endpos := matchposition(p_phrase) + matchlength(p_phrase) - 1;
	end;

	DS_Phrases := dataset([
		{1,'DISTRICT OF COLUMBIA','DC'},
		{2,'FEDERAL CREDIT UNION','FCU'},
		{3,'NATIONAL ASSOCIATION','NA'},
		{4,'CORPORPORATION','CORP'},
		{5,'NORTH CAROLINA','NC'},
		{6,'SOUTH CAROLINA','SC'},
		{7,'CORPORORATION','CORP'},
		{8,'INCORPORATING','CORP'},
		{9,'INCORPORATION','CORP'},
		{262,'MANUFACTURING','MFG'},
		{10,'MASSACHUSETTS','MA'},
		{11,'NEW HAMPSHIRE','NH'},
		{12,'WEST VIRGINIA','WV'},
		{13,'CONSTRUCTION','CONST'},
		{14,'CORPORATIONN','CORP'},
		{15,'CORPORATIONS','CORP'},
		{16,'CORPORATIOON','CORP'},
		{17,'CORPORATISON','CORP'},
		{18,'CORPORATRION','CORP'},
		{19,'CORPORATTION','CORP'},
		{20,'CORPOREATION','CORP'},
		{21,'CORPOROATION','CORP'},
		{22,'CORPORRATION','CORP'},
		{23,'CORPORTAITON','CORP'},
		{24,'CORPORTATION','CORP'},
		{25,'CORPPORATION','CORP'},
		{26,'CORPRORATION','CORP'},
		{27,'CORRPORATION','CORP'},
		{28,'INCORPORATED','INC'},
		{29,'NORTH DAKOTA','ND'},
		{30,'PENNSYLVANIA','PA'},
		{31,'RHODE ISLAND','RI'},
		{32,'SOUTH DAKOTA','SD'},
		{33,'CONNECTICUT','CT'},
		{34,'COOPERATIVE','COOP'},
		{35,'CORPORATION','CORP'},
		{36,'CORPORATOIN','CORP'},
		{37,'CORPORATTON','CORP'},
		{38,'CORPORSTION','CORP'},
		{39,'CORPORTAION','CORP'},
		{40,'CORPORTIAON','CORP'},
		{41,'CORPOTATION','CORP'},
		{42,'CORPPRATION','CORP'},
		{43,'CORPROATION','CORP'},
		{44,'CPRPORATION','CORP'},
		{45,'CROPORATION','CORP'},
		{46,'DEPARTMENTS','DEPT'},
		{47,'ENTERPRISES','ENT'},
		{48,'MISSISSIPPI','MS'},
		{49,'OCRPORATION','CORP'},
		{50,'OPRPORATION','CORP'},
		{51,'SORPORATION','CORP'},
		{52,'ACCPETANCE','ACCEPTANCE'},
		{53,'APARTMENTS','APT'},
		{54,'CALIFORNIA','CA'},
		{55,'CORPORATON','CORP'},
		{56,'CORPORTION','CORP'},
		{57,'CORPOTIION','CORP'},
		{58,'CORPRATION','CORP'},
		{59,'CRPORATION','CORP'},
		{60,'DEPARTMENT','DEPT'},
		{61,'ENTERPRISE','ENT'},
		{62,'FINANCIALL','FINANCIAL'},
		{63,'FINANCIALS','FINANCIAL'},
		{64,'FINANCIAOL','FINANCIAL'},
		{65,'FINANCICAL','FINANCIAL'},
		{66,'FINANCIING','FINANCIAL'},
		{67,'FINANCINAL','FINANCIAL'},
		{68,'FINANCIUAL','FINANCIAL'},
		{69,'FINANCVIAL','FINANCIAL'},
		{70,'FINANFCIAL','FINANCIAL'},
		{71,'FINANICIAL','FINANCIAL'},
		{72,'FINANNCIAL','FINANCIAL'},
		{73,'FINCANCIAL','FINANCIAL'},
		{74,'FINCANICAL','FINANCIAL'},
		{75,'FINIANCIAL','FINANCIAL'},
		{76,'FINNANCIAL','FINANCIAL'},
		{77,'FIONANCIAL','FINANCIAL'},
		{78,'MANAGEMENT','MGMT'},
		{79,'NATL ASSOC','NA'},
		{80,'NEW JERSEY','NJ'},
		{81,'NEW MEXICO','NM'},
		{82,'ORPORATION','CORP'},
		{83,'WASHINGTON','WA'},
		{84,'APARTMENT','APT'},
		{85,'COMPANIES','CO'},
		{86,'COOMMPANY','CO'},
		{87,'COOOMPANY','CO'},
		{88,'CORPORATN','CORP'},
		{89,'CORPORTAI','CORP'},
		{90,'CORPORTIA','CORP'},
		{91,'CORPORTIO','CORP'},
		{92,'CREDITORS','CREDIT'},
		{93,'EQUIPMENT','EQUIP'},
		{94,'FINANCIAN','FINANCIAL'},
		{95,'FINANCIEL','FINANCIAL'},
		{96,'FINANCILA','FINANCIAL'},
		{97,'FINANCING','FINANCIAL'},
		{98,'FINANCISL','FINANCIAL'},
		{99,'FINANCOAL','FINANCIAL'},
		{100,'FINANCUAL','FINANCIAL'},
		{101,'FINANDIAL','FINANCIAL'},
		{102,'FINANICAL','FINANCIAL'},
		{103,'FINANVIAL','FINANCIAL'},
		{104,'FINCNCIAL','FINANCIAL'},
		{105,'FININCIAL','FINANCIAL'},
		{106,'FINNACIAL','FINANCIAL'},
		{107,'FLNANCIAL','FINANCIAL'},
		{108,'FUNANCIAL','FINANCIAL'},
		{109,'IFNANCIAL','FINANCIAL'},
		{110,'LOUISIANA','LA'},
		{111,'MINNESOTA','MN'},
		{112,'NATL ASSN','NA'},
		{113,'RPORATION','CORP'},
		{114,'TENNESSEE','TN'},
		{115,'WISCONSIN','WI'},
		{116,'ARKANSAS','AR'},
		{117,'BROTHERS','BROS'},
		{118,'COLORADO','CO'},
		{119,'COMPANYS','CO'},
		{120,'COMPANYY','CO'},
		{121,'COMPAPNY','CO'},
		{122,'COMPARNY','CO'},
		{123,'COMPASNY','CO'},
		{124,'COMPLANY','CO'},
		{125,'COMPNANY','CO'},
		{126,'COMPOANY','CO'},
		{127,'COMPPANY','CO'},
		{128,'CONMPANY','CO'},
		{129,'COOMPANY','CO'},
		{130,'COPMPANY','CO'},
		{131,'CORMPANY','CO'},
		{132,'CORPROAT','CORP'},
		{133,'DELAWARE','DE'},
		{134,'DIVISION','DIV'},
		{135,'FINANCIL','FINANCIAL'},
		{136,'FINANCIN','FINANCIAL'},
		{137,'FINANCLA','FINANCIAL'},
		{138,'FINANIAL','FINANCIAL'},
		{139,'FINNCIAL','FINANCIAL'},
		{140,'FNANCIAL','FINANCIAL'},
		{141,'ILLINOIS','IL'},
		{142,'INANCIAL','FINANCIAL'},
		{143,'KENTUCKY','KY'},
		{144,'MARYLAND','MD'},
		{145,'MICHIGAN','MI'},
		{261,'MID WEST','MIDWEST'},
		{146,'MISSOURI','MO'},
		{147,'NATIONAL','NATL'},
		{148,'NEBRASKA','NE'},
		{149,'NEW YORK','NY'},
		{150,'OKLAHOMA','OK'},
		{151,'PORATION','CORP'},
		{152,'SERVICES','SVC'},
		{153,'VIRGINIA','VA'},
		{154,'ALABAMA','AL'},
		{155,'ARIZONA','AZ'},
		{156,'BANKERS','BK'},
		{157,'BANKING','BK'},
		{158,'CENTERS','CTR'},
		{159,'COMPANY','CO'},
		{160,'COMPNAY','CO'},
		{161,'COMPONY','CO'},
		{162,'COMPSNY','CO'},
		{163,'COMRANY','CO'},
		{164,'CONPANY','CO'},
		{165,'COPMANY','CO'},
		{166,'CPMPANY','CO'},
		{167,'CREDITS','CREDIT'},
		{168,'CREDITT','CREDIT'},
		{169,'CREDIUT','CREDIT'},
		{170,'CREDOIT','CREDIT'},
		{171,'CREDSIT','CREDIT'},
		{172,'CREEDIT','CREDIT'},
		{173,'CREIDIT','CREDIT'},
		{174,'CRERDIT','CREDIT'},
		{175,'CRFEDIT','CREDIT'},
		{176,'CRREDIT','CREDIT'},
		{177,'CRTEDIT','CREDIT'},
		{178,'DOMPANY','CO'},
		{179,'FINANCL','FINANCIAL'},
		{180,'FINCIAL','FINANCIAL'},
		{181,'FLORIDA','FL'},
		{182,'GEORGIA','GA'},
		{183,'INDIANA','IN'},
		{184,'MONTANA','MT'},
		{185,'OCMPANY','CO'},
		{186,'SERVICE','SVC'},
		{187,'VERMONT','VT'},
		{188,'WYOMING','WY'},
		{189,'ALASKA','AK'},
		{190,'AVENUE','AV'},
		{191,'BANKER','BK'},
		{192,'CENTER','CTR'},
		{193,'COMPAY','CO'},
		{194,'COMPNY','CO'},
		{195,'COPANY','CO'},
		{196,'CREDOT','CREDIT'},
		{197,'CREDTI','CREDIT'},
		{198,'CREDTT','CREDIT'},
		{199,'CREDUT','CREDIT'},
		{200,'CREEIT','CREDIT'},
		{201,'CREIDT','CREDIT'},
		{202,'CRESIT','CREDIT'},
		{203,'CRIDIT','CREDIT'},
		{204,'CRRDIT','CREDIT'},
		{205,'CTEDIT','CREDIT'},
		{206,'HAWAII','HI'},
		{207,'KANSAS','KS'},
		{208,'NEVADA','NV'},
		{209,'OMPANY','CO'},
		{210,'OREGON','OR'},
		{211,'STREET','ST'},
		{212,'BANKE','BK'},
		{213,'BANKK','BK'},
		{214,'BANKS','BK'},
		{215,'BANMK','BK'},
		{216,'BANNK','BK'},
		{217,'BAQNK','BK'},
		{218,'BASNK','BK'},
		{219,'BBANK','BK'},
		{220,'BNANK','BK'},
		{221,'BVANK','BK'},
		{222,'COMPY','CO'},
		{223,'CREDT','CREDIT'},
		{224,'CREIT','CREDIT'},
		{225,'D D S','DDS'},
		{226,'DEPTS','DEPT'},
		{227,'FIRST','1ST'},
		{228,'IDAHO','ID'},
		{229,'L L C','LLC'},
		{230,'MAINE','ME'},
		{231,'MOUNT','MT'},
		{232,'NORTH','N'},
		{233,'SAINT','ST'},
		{234,'SOUTH','S'},
		{235,'TEXAS','TX'},
		{236,'APTS','APT'},
		{237,'BANK','BK'},
		{238,'BNAK','BK'},
		{239,'BNKG','BK'},
		{240,'CTRS','CTR'},
		{241,'EAST','E'},
		{242,'ENTS','ENT'},
		{243,'FORT','FT'},
		{244,'IOWA','IA'},
		{245,'OHIO','OH'},
		{246,'ROAD','RD'},
		{247,'SVCS','SVC'},
		{248,'UTAH','UT'},
		{249,'WEST','W'},
		{250,'AVE','AV'},
		{251,'BNK','BK'},
		{252,'N A','NA'},
		{253,'N E','NE'},
		{254,'N W','NW'},
		{255,'S E','SE'},
		{256,'S W','SW'},
		{257,'&','AND'},
		{258,'CHILD CARE','CHLDCR'},
		{259,'CHILDCARE','CHLDCR'},
		{260,'LIMITED PARTNERSHIP','LP'},
		{263,'VETERANS FGN WARS','VFW'},
		{264,'VETERANS OF FOREIGN WARS OF THE UNITED STATES','VFW'},
		{265,'VETERANS OF FOREIGN WARS','VFW'},
		{266,'MAINTENANCE','MAINT'},
		{267,'LAW OFFICES OF','ATTY'},
		{268,'ATTORNEY AT LAW','ATTY'},
		{269,'LAWYER','ATTY'},
		{270,'ATTORNEYS AT LAW','ATTY'},
		{271,'LAW OFFICES','ATTY'},
		{276,'LAW OFFICE OF','ATTY'},
		{277,'LAW OFFICE','ATTY'},
		{272,'ATTORNEYS','ATTY'},
		{273,'ATTORNEY','ATTY'},
		{275,'ATTY AT LAW','ATTY'},
		{274,'ATT AT LAW','ATTY'},
		{278,'TWO','2'},
		{279,'II','2'},
		{280,'THREE','3'},
		{281,'III','3'},
		{282,'LABORATORIES','LAB'},
		{283,'LABORATORY','LAB'},
		{284,'LABS','LAB'},
		{285,'REMEDITION','REMEDIATION'}],{unsigned phrase_priority,string find_phrase,string repl_phrase});


	tempparse := distributed(parse(unique_company_names,clean_company_name,p_search,tempparserec,scan all),hash64(clean_company_name));
	
	tempjoinrec := record
		tempparserec.clean_company_name;
		DS_Phrases.phrase_priority;
		tempparserec.phrase;
		tempparserec.startpos;
		tempparserec.endpos;
		DS_Phrases.repl_phrase;
	end;

	tempparsejoin := join(tempparse,DS_Phrases,
		left.phrase = right.find_phrase,
		transform(tempjoinrec,
			self.phrase_priority := right.phrase_priority,
			self.repl_phrase := if(right.phrase_priority = 0,left.phrase,right.repl_phrase),
			self := left),
		left outer,
		lookup);

	tempsort := sort(tempparsejoin,clean_company_name,if(phrase_priority = 0,1,0),phrase_priority,startpos,-endpos,local);

	tempsequence := ungroup(project(group(tempsort,clean_company_name,local),transform({unsigned seq;tempsort;},self.seq := counter,self := left)));

	submacro(din,tout,rout) := macro
		tout := dedup(sorted(din,clean_company_name,seq,local),clean_company_name,local);
		rout := join(din,tout,
			left.clean_company_name = right.clean_company_name and
			(
				(left.startpos < right.startpos and left.endpos < right.startpos) or
				(left.startpos > right.endpos and left.endpos > right.endpos)
			),
			transform(left),
			local);
	endmacro;
	
	// Take the tops for each
	submacro(tempsequence,tops_1,remove_1);
	submacro(remove_1,tops_2,remove_2);
	submacro(remove_2,tops_3,remove_3);
	submacro(remove_3,tops_4,remove_4);
	submacro(remove_4,tops_5,remove_5);
	submacro(remove_5,tops_6,remove_6);
	submacro(remove_6,tops_7,remove_7);
	submacro(remove_7,tops_8,remove_8);
	
	return dedup(project(rollup(sort(tops_1 + tops_2 + tops_3 + tops_4 + tops_5 + tops_6 + tops_7 + tops_8,clean_company_name,startpos,local),
		transform(recordof(tops_8),
			self.repl_phrase := left.repl_phrase + ' ' + right.repl_phrase,
			self := left),
		clean_company_name,local),recordof(unique_company_names)) + unique_company_names,record,all,local);

end;
